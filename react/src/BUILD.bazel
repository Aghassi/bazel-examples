load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:eslint/package_json.bzl", eslint_bin = "bin")
load("@jest//:defs.bzl", "jest_test")

ts_project(
    name = "src_ts",
    srcs = glob(
        include = [
            "*.tsx",
            "*.ts",
        ],
        exclude = [
            "*.test.tsx",
            "*.test.ts",
        ],
    ),
    declaration = True,
    resolve_json_module = True,
    supports_workers = False,
    tsconfig = "//:tsconfig",
    visibility = ["//visibility:public"],
    deps = [
        "//:node_modules/react",
        "//:node_modules/react-dom",
        "//:node_modules/web-vitals",
    ],
)

ts_project(
    name = "src_test_ts",
    srcs = glob(
        include = [
            "*.test.tsx",
            "*.test.ts",
        ],
    ),
    declaration = True,
    resolve_json_module = True,
    supports_workers = False,
    tsconfig = "//:tsconfig",
    visibility = ["//visibility:public"],
    deps = [
        ":src_ts",
        "//:node_modules/@testing-library/jest-dom",
        "//:node_modules/@testing-library/react",
        "//:node_modules/@types/jest",
    ],
)

jest_test(
    name = "src_test",
    config = "//:jest_config",
    data = [
        ":src_test_ts",
        "//:node_modules/jest-transform-stub",
        "//:node_modules/jest-environment-jsdom",
    ] + glob([
        "*.svg",
        "*.css",
    ]),
)

# NB: this test target requires sandboxed execution to work or else eslint fails with.
# ```
# Oops! Something went wrong! :(
# ESLint: 8.23.0
# ESLint couldn't determine the plugin "@typescript-eslint" uniquely.
# - /private/var/tmp/_bazel_greg/10318978e5dd7e5d402fb94fcc20c62a/execroot/__main__/bazel-out/darwin-fastbuild/bin/src/eslint_test.sh.runfiles/__main__/node_modules/.aspect_rules_js/@typescript-eslint+eslint-plugin@5.40.0_kboqmcjzzujdr4gehckwcgn4m4/node_modules/@typescript-eslint/eslint-plugin/dist/index.js (loaded in "--config")
# - /private/var/tmp/_bazel_greg/10318978e5dd7e5d402fb94fcc20c62a/execroot/__main__/bazel-out/darwin-fastbuild/bin/node_modules/.aspect_rules_js/@typescript-eslint+eslint-plugin@5.40.0_kboqmcjzzujdr4gehckwcgn4m4/node_modules/@typescript-eslint/eslint-plugin/dist/index.js (loaded in "../../../.eslintrc.json")
# Please remove the "plugins" setting from either config or remove either plugin installation.
# If you still can't figure out the problem, please stop by https://eslint.org/chat/help to chat with the team.
# ```
# Since `bazel run` of a test target does not use sandboxed execution it cannot be used with this target.
# If you want to see the output of the target is at executes run,\
# `bazel test //src:eslint_test --test_output=streamed --nocache_test_result` instead.
eslint_bin.eslint_test(
    name = "eslint_test",
    args = [
        "--config .eslintrc.json",
        "{}/*.tsx".format(package_name()),
        "{}/*.ts".format(package_name()),
    ],
    data = [
        "//:eslintrc",
        "//:node_modules/@typescript-eslint",
        # also include node_modules/eslint in execroot so @typescript-eslint code can find it
        "//:node_modules/eslint",
    ] + glob([
        "*.ts",
        "*.tsx",
    ]),
)
